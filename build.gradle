import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'eclipse'
    id 'com.geoffgranum.gradle-conventional-changelog' version '0.3.1'
}
apply plugin: 'net.minecraftforge.gradle'

def prevVersion = '1.0.0'
def baseVersion = '1.1.0'
def minecraftVersion = '1.16.5'
version = "${minecraftVersion}-${baseVersion}" as Object
group = 'io.github.samarium150'
archivesBaseName = 'taghelper'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

logger.quiet('Java: {} JVM: {}({}) Arch: {}',
        System.getProperty('java.version'),
        System.getProperty('java.vm.version'),
        System.getProperty('java.vendor'),
        System.getProperty('os.arch')
)

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}
def mainSourceSet = sourceSets.main

minecraft {
    mappings channel: 'official', version: '1.16.5'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                structurescompass {
                    source mainSourceSet
                }
            }
        }
        server {
            workingDirectory file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                structurescompass {
                    source mainSourceSet
                }
            }
        }
    }
}

// Include resources generated by data generators.
// sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.22'
    implementation 'com.google.code.gson:gson:2.8.9'
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
}

tasks.named("jar").configure {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "Samarium",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"Samarium",
                "Implementation-Timestamp": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
        ])
    }
}
jar.finalizedBy('reobfJar')

changelog {
    appName "${project.name}"
    versionNum "${project.version}"
    from "${minecraftVersion}-${prevVersion}"
    repoUrl "https://github.com/Samarium150/TagHelper"
    trackerUrl "https://github.com/Samarium150/TagHelper/issues"
}

javadoc {
    options.locale = "en_US"
    options.encoding = "UTF-8"
}